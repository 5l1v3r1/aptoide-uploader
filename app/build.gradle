buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Should be moved to application module when possible
Properties localProperties = new Properties()
localProperties.load(new FileInputStream("local.properties"))

def gmsServerId = localProperties.GMS_SERVER_ID
def fabricApiKey = localProperties.FABRIC_API_KEY
def fabricConfigured = fabricApiKey != null
if (fabricConfigured) {
    task fabricGenerateProperties {
        doFirst {
            def fabricProperties = project.file("fabric.properties")
            fabricProperties.delete()
            fabricProperties.append("# Do not modify this file -- YOUR CHANGES WILL BE ERASED!\n")
            fabricProperties.append("apiKey=" + fabricApiKey)
        }
    }
    preBuild.dependsOn(fabricGenerateProperties)
    apply plugin: 'io.fabric'
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    signingConfigs { release }

    def BOOLEAN = "boolean"
    def STRING = "String"

    def GMS_CONFIGURED = "GMS_CONFIGURED"
    def GMS_SERVER_ID = "GMS_SERVER_ID"
    def FACEBOOK_CONFIGURED = "FACEBOOK_CONFIGURED"
    def FABRIC_CONFIGURED = "FABRIC_CONFIGURED"

    defaultConfig {
        applicationId "pt.caixamagica.aptoide.uploader"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 22
        versionName "2.1"

        buildConfigField BOOLEAN, GMS_CONFIGURED, (gmsServerId != null).toString()
        buildConfigField STRING, GMS_SERVER_ID, "\"" + gmsServerId.toString() + "\""

        buildConfigField BOOLEAN, FABRIC_CONFIGURED, (fabricApiKey != null).toString()

        def facebookAppId = localProperties.FACEBOOK_APP_ID
        def facebookAppIdExists = facebookAppId != null
        buildConfigField BOOLEAN, FACEBOOK_CONFIGURED, facebookAppIdExists.toString()
        facebookAppId = facebookAppIdExists ? facebookAppId.toString() : "0"
        resValue "string", "facebook_app_id", "\"" + facebookAppId + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    project.ext { appName = 'aptoide-uploader' }
                    def newName = output.outputFile.name
                    newName = newName.replace("app-", "$project.ext.appName-") //"MyAppName" -> I set my app variables in the root project
                    newName = newName.replace("-release", "-" + versionName)
                    //noinspection GroovyAssignabilityCheck
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
    }
    useLibrary 'org.apache.http.legacy'

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.2.1'

    compile 'com.octo.android.robospice:robospice:1.4.14'
    compile 'com.octo.android.robospice:robospice-retrofit:1.4.14'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.4'
    compile 'com.squareup.retrofit:converter-jackson:1.3.0'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.github.manuelpeinado.multichoiceadapter:multichoiceadapter:3.1.0'
    compile 'com.facebook.android:facebook-android-sdk:3.23.0'
    compile 'com.google.android.gms:play-services:6.5.87'
    provided 'org.projectlombok:lombok:1.16.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
}

def File propFile = new File('signing.properties')
if (propFile.canRead()) {
    def Properties props = new Properties()
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        println 'all good to go'
    } else {
        android.buildTypes.release.signingConfig = null
        println 'signing.properties found but some entries are missing'
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}